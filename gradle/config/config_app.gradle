apply {
    plugin "com.android.application"
    plugin "kotlin-android"
    plugin "kotlin-android-extensions"
}

configSigning project
configApkName project

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        applicationId rootProject.applicationId + suffix
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        multiDexEnabled true
        resValue "string", "app_name", rootProject.appName + suffix

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}


dependencies {
    // LeakCanary
    debugImplementation dep.leakcanary.android
    debugImplementation dep.leakcanary.support_fragment
    releaseImplementation dep.leakcanary.android_no_op
}

private String getSuffix() {
    return "" //project.name
}

def configSigning(Project pro) {

    File signPropertiesFile = file("${rootDir.path}/sign/keystore.properties")
    if (!signPropertiesFile.exists()) return

    println "$pro.name config sign start..."
    pro.android {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signPropertiesFile))
        signingConfigs {
            release {
                storeFile new File(signPropertiesFile.getParent(), properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }
    println "$pro.name config sign end..."
}

def configApkName(Project pro) {
    pro.android.applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
//            variant.getPackageApplication().outputDirectory = new File("${rootDir.path}/apk")
//            variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
//                apkData.outputFileName = rootProject.name +
//                                         "_" + variant.versionName.replace(".", "_") +
//                                         ".apk"
//            }

            variant.getPackageApplicationProvider().get().outputDirectory = new File("${rootDir.path}/apk")
            variant.outputs.all { output ->
                outputFileName = rootProject.name +
                        "_" + variant.versionName.replace(".", "_") +
                        ".apk"
            }
        }
    }
}